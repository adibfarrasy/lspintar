        allprojects {
            afterEvaluate {
                if (plugins.hasPlugin('java') || plugins.hasPlugin('groovy')) {
                    task lspClasspath {
                        doLast {
                            def allJars = (configurations.compileClasspath.files + configurations.runtimeClasspath.files).unique()
                            allJars.each {
                                println it.absolutePath
                            }
                        }
                    }
                    
                    task lspSources {
                        doLast {
                            def allArtifacts = (configurations.compileClasspath.resolvedConfiguration.resolvedArtifacts + 
                                configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts).unique()

                            allArtifacts.each { artifact ->
                                def id = artifact.moduleVersion.id
                                try {
                                    def dep = dependencies.create("${id.group}:${id.name}:${id.version}:sources")
                                    def sourceConfig = configurations.detachedConfiguration(dep)
                                    sourceConfig.files.each { sourceJar ->
                                        println sourceJar.absolutePath
                                    }
                                } catch (Exception e) {
                                    // Source not available, skip
                                }
                            }
                        }
                    }
                }
            }
        }
